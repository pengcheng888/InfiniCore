#include "../../../devices/nvidia/nvidia_common.cuh"
#include "../../../devices/nvidia/nvidia_kernel_common.cuh"
#include "mamba_selective_scan_nvidia.cuh"
#include <cuda_bf16.h>
#include <cuda_fp16.h>
#include <cuda_runtime.h>

namespace op::mamba_selective_scan::nvidia {

struct Descriptor::Opaque {
    std::shared_ptr<device::nvidia::Handle::Internal> internal;
};
Descriptor::~Descriptor() { delete _opaque; }

namespace {
template <typename T>
__device__ float to_float(T v) { return static_cast<float>(v); }
template <>
__device__ float to_float<half>(half v) { return __half2float(v); }
template <>
__device__ float to_float<__nv_bfloat16>(__nv_bfloat16 v) { return __bfloat162float(v); }
template <typename T>
__device__ T from_float(float v) { return static_cast<T>(v); }
template <>
__device__ half from_float<half>(float v) { return __float2half_rn(v); }
template <>
__device__ __nv_bfloat16 from_float<__nv_bfloat16>(float v) { return __float2bfloat16_rn(v); }
__device__ float softplusf_stable(float x) { return x > 20.0f ? x : log1pf(expf(x)); }
__device__ float siluf(float x) { return x / (1.0f + expf(-x)); }

template <typename T>
__global__ void mamba_selective_scan_kernel(
    T *out, const T *x, const T *dt, const T *b, const T *c, const T *a_log,
    const T *d, const T *gate, const T *dt_bias, float *state,
    size_t batch, size_t seq_len, size_t intermediate, size_t state_size) {
    size_t linear = blockIdx.x * blockDim.x + threadIdx.x;
    size_t total = batch * intermediate;
    if (linear >= total) {
        return;
    }
    size_t ch = linear % intermediate;
    size_t batch_idx = linear / intermediate;
    size_t state_base = (batch_idx * intermediate + ch) * state_size;
    for (size_t t = 0; t < seq_len; ++t) {
        size_t x_idx = (batch_idx * seq_len + t) * intermediate + ch;
        float xt = to_float<T>(x[x_idx]);
        float dtv = softplusf_stable(to_float<T>(dt[x_idx]) + to_float<T>(dt_bias[ch]));
        float y = 0.0f;
        for (size_t n = 0; n < state_size; ++n) {
            float a = -expf(to_float<T>(a_log[ch * state_size + n]));
            float discrete_a = expf(a * dtv);
            float bn = to_float<T>(b[(batch_idx * seq_len + t) * state_size + n]);
            float cn = to_float<T>(c[(batch_idx * seq_len + t) * state_size + n]);
            float s = discrete_a * state[state_base + n] + dtv * bn * xt;
            state[state_base + n] = s;
            y += s * cn;
        }
        y += xt * to_float<T>(d[ch]);
        y *= siluf(to_float<T>(gate[x_idx]));
        out[x_idx] = from_float<T>(y);
    }
}

template <typename T>
infiniStatus_t launch_typed(const MambaSelectiveScanInfo &info, void *out, const void *x,
                            const void *dt, const void *b, const void *c, const void *a_log,
                            const void *d, const void *gate, const void *dt_bias, void *state,
                            cudaStream_t stream) {
    constexpr int threads = 256;
    size_t total = info.batch * info.intermediate;
    dim3 blocks((total + threads - 1) / threads);
    mamba_selective_scan_kernel<T><<<blocks, threads, 0, stream>>>(
        reinterpret_cast<T *>(out), reinterpret_cast<const T *>(x), reinterpret_cast<const T *>(dt),
        reinterpret_cast<const T *>(b), reinterpret_cast<const T *>(c), reinterpret_cast<const T *>(a_log),
        reinterpret_cast<const T *>(d), reinterpret_cast<const T *>(gate), reinterpret_cast<const T *>(dt_bias),
        reinterpret_cast<float *>(state), info.batch, info.seq_len, info.intermediate, info.state_size);
    CHECK_CUDA(cudaGetLastError());
    return INFINI_STATUS_SUCCESS;
}
} // namespace

infiniStatus_t Descriptor::create(infiniopHandle_t handle, Descriptor **desc_ptr,
                                  infiniopTensorDescriptor_t out_desc, infiniopTensorDescriptor_t x_desc,
                                  infiniopTensorDescriptor_t dt_desc, infiniopTensorDescriptor_t b_desc,
                                  infiniopTensorDescriptor_t c_desc, infiniopTensorDescriptor_t a_log_desc,
                                  infiniopTensorDescriptor_t d_desc, infiniopTensorDescriptor_t gate_desc,
                                  infiniopTensorDescriptor_t dt_bias_desc, infiniopTensorDescriptor_t state_desc) {
    auto result = MambaSelectiveScanInfo::create(out_desc, x_desc, dt_desc, b_desc, c_desc, a_log_desc, d_desc, gate_desc, dt_bias_desc, state_desc);
    CHECK_RESULT(result);
    auto info = result.take();
    *desc_ptr = new Descriptor(new Opaque{reinterpret_cast<device::nvidia::Handle *>(handle)->internal()}, info, 0, handle->device, handle->device_id);
    return INFINI_STATUS_SUCCESS;
}

infiniStatus_t Descriptor::calculate(void *workspace, size_t workspace_size, void *out,
                                     const void *x, const void *dt, const void *b, const void *c,
                                     const void *a_log, const void *d, const void *gate,
                                     const void *dt_bias, void *state, void *stream_) const {
    (void)workspace;
    if (workspace_size < _workspace_size) {
        return INFINI_STATUS_INSUFFICIENT_WORKSPACE;
    }
    auto stream = reinterpret_cast<cudaStream_t>(stream_);
    if (_info.dtype == INFINI_DTYPE_F16) {
        return launch_typed<half>(_info, out, x, dt, b, c, a_log, d, gate, dt_bias, state, stream);
    }
    if (_info.dtype == INFINI_DTYPE_BF16) {
        return launch_typed<__nv_bfloat16>(_info, out, x, dt, b, c, a_log, d, gate, dt_bias, state, stream);
    }
    if (_info.dtype == INFINI_DTYPE_F32) {
        return launch_typed<float>(_info, out, x, dt, b, c, a_log, d, gate, dt_bias, state, stream);
    }
    return INFINI_STATUS_BAD_TENSOR_DTYPE;
}

} // namespace op::mamba_selective_scan::nvidia
